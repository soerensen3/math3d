{
The MIT License (MIT)

Copyright (c) [2015] [Johannes Rosleff Soerensen]

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
}

{$IFDEF INTERFACE}
type
  //@abbr`Quaternions can be used instead of or together with matrices for transforming the view`.
  TQuaternion = object( TVector )
    // Using TVector instead of TVec4 as descendent so it can be easily
    // replaced with double vectors for higher precision if necessary in the future
    public
      function Conj: TQuaternion;
      function GetAxisAngle: TVec4; //with XYZ as Axis and A (=W) as angle
  end;

  TRotationOrder = ( roXYZ = 0, roXZY = 1, roYXZ = 2, roYZX = 3, roZXY = 4, roZYX = 5 );

//Multiplies two quaternions.
operator * ( q1, q2: TQuaternion ) q : TQuaternion;
//Transforms a vector using a quaternion.
operator * ( a: TQuaternion; b: TVector ) c: TVector;
//Inverts a quaternion.
operator - ( a: TQuaternion ) b: TQuaternion;

function quat( vec: TVector ): TQuaternion;
function quat(x, y, z, w: Float): TQuaternion;

{$ENDIF}
{$IFDEF IMPLEMENTATION}
{ TQuaternion }

function TQuaternion.Conj: TQuaternion;
begin
  Result.w:= w;
  Result.x:= -x;
  Result.y:= -y;
  Result.z:= -z;
end;

function TQuaternion.GetAxisAngle: TVec4;
var
  q1: TQuaternion;
  sp: Single;
begin
  q1:= Self;
  if ( q1.w > 1) then
    q1.Normalize(); // if w>1 acos and sqrt will produce errors, this cant happen if quaternion is normalised
  Result.w:= 2 * arccos( q1.w );
  sp:= sqrt( 1 - q1.w * q1.w ); // assuming quaternion normalised then w is less than 1, so term always positive.
  if ( sp < 0.001 ) then // test to avoid divide by zero, sp is always positive due to sqrt
    // if sp close to zero then direction of axis not important
    begin
      x:= q1.x; // if it is important that axis is normalised then replace with x=1; y=z=0;
      y:= q1.y;
      z:= q1.z;
    end
  else
    begin
      x:= q1.x / sp; // normalise axis
      y:= q1.y / sp;
      z:= q1.z / sp;
    end;
end;

operator*(q1, q2: TQuaternion)q: TQuaternion;
begin
  q.w:=   q1.w * q2.w
        - q1.x * q2.x
        - q1.y * q2.y
        - q1.z * q2.z;
  q.x:=   q1.w * q2.x
        + q1.x * q2.w
        + q1.y * q2.z
        - q1.z * q2.y;
  q.y:=   q1.w * q2.y
        - q1.x * q2.z
        + q1.y * q2.w
        + q1.z * q2.x;
  q.z:=   q1.w * q2.z
        + q1.x * q2.y
        - q1.y * q2.x
        + q1.z * q2.w;
end;

{$WARNINGS OFF} // Omit warnings about the function result variable not being initialized
operator * ( a: TQuaternion; b: TVec4 ) c: TVec4;
var
  a00, a01, a02, a03,
  a11, a12, a13, a22,
  a23, a33: Float;
begin
  a00:= a[0] * a[0];
  a01:= a[0] * a[1];
  a02:= a[0] * a[2];
  a03:= a[0] * a[3];
  a11:= a[1] * a[1];
  a12:= a[1] * a[2];
  a13:= a[1] * a[3];
  a22:= a[2] * a[2];
  a23:= a[2] * a[3];
  a33:= a[3] * a[3];

  c[0]:= b.x * ( +a00 + a11 - a22 - a33 )
        + 2 * ( a12 * b.y + a13 * b.z + a02 * b.z - a03 * b.y );
  c[1]:= b.y * ( +a00 - a11 + a22 - a33 )
        + 2 * ( a12 * b.x + a23 * b.z + a03 * b.x - a01 * b.z );
  c[2]:= b.z * ( +a00 - a11 - a22 + a33 )
        + 2 * ( a13 * b.x + a23 * b.y - a02 * b.x + a01 * b.y );

  c[3]:= 1;
end;
{$WARNINGS ON}

{$HINTS OFF} // Omit hints about the function result variable not being initialized
operator -( a: TQuaternion ) b: TQuaternion;
var
  len: Single;
begin
  len:= a.GetDist;
  len:= len * len;
  if ( len = 0.0 ) then
    FillByte( b, SizeOf( b ), 0 )
  else
    b:= quat( TVector( a.Conj()) * ( 1.0 / len ));
end;

function quat(vec: TVec4): TQuaternion;
begin
  Result.w:= vec.w;
  Result.x:= vec.x;
  Result.y:= vec.y;
  Result.z:= vec.z;
end;

function quat(x, y, z, w: Float): TQuaternion;
begin
  Result.w:= w;
  Result.x:= x;
  Result.y:= y;
  Result.z:= z;
end;

function quatRotation( Axis: TVec3; Alpha: Float ): TQuaternion;
begin
  Result:= quat( Axis.x * sin( Alpha/2 ), Axis.y * sin( Alpha/2 ), Axis.z * sin( Alpha/2 ), cos( Alpha/2 ));
end;





{$WARNINGS ON}

{$ENDIF}
